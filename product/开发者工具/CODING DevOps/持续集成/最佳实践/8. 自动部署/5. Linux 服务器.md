本文为您介绍如何通过持续集成将项目发布至 Linux 服务器。

## 前提条件
设置 CODING 持续集成中构建环境前，您的腾讯云账号需要开通 CODING DevOps 服务，详情请参见 [开通服务](https://cloud.tencent.com/document/product/1115/37268)。

## 进入项目
1. 登录 [CODING 控制台](https://console.cloud.tencent.com/coding)，单击**团队域名**进入 CODING 使用页面。
2. 单击页面右上角的 <img src ="https://main.qcloudimg.com/raw/d94a8e60dd3a41d0af07d72ae0e9d70e.png" style ="margin:0">，进入项目列表页面，单击**项目图标**进入目标项目。
3. 进入左侧菜单栏的**持续集成**功能。

## 功能介绍
持续集成可简易部署项目到单台 Linux 服务器（多台建议使用 [持续部署](https://cloud.tencent.com/document/product/1159/38995)），步骤如下：
1. 在云计算（例如：腾讯云）的网页控制台创建 SSH 密钥对，把私钥录入 CODING 凭据管理，把公钥加载到服务器。也可以使用命令手动创建，把私钥 id_rsa 录入 CODING 凭据管理，把公钥 id_rsa.pub 的内容复制到服务器的 `~/.ssh/authorized_keys`，参考文档：[凭据管理](https://cloud.tencent.com/document/product/1113/48846)。
![](https://qcloudimg.tencent-cloud.cn/raw/7ec078a080d2ce1c547d33ee2ab5148f.png)
![](https://qcloudimg.tencent-cloud.cn/raw/ef3c3eae23a027dd98168d970542ab3a.png)
![](https://qcloudimg.tencent-cloud.cn/raw/dfd95038d4d38a36610b8f0d1d64f5e6.png)
![](https://qcloudimg.tencent-cloud.cn/raw/196044e93d474abd01d564289e369547.png)

2. 在持续集成中使用下述 Jenkinsfile：执行 SSH 进行部署。

## Jenkinsfile
```groovy
pipeline {
  agent any
  stages {
    stage('检出') {
      steps {
        checkout(
          [$class: 'GitSCM', branches: [[name: env.GIT_BUILD_REF]],
          userRemoteConfigs: [[url: env.GIT_REPO_URL, credentialsId: env.CREDENTIALS_ID]]]
        )
      }
    }
    stage('构建') {
      steps {
        echo '构建中...'
        // Java Spring Boot Gradle Jar
        // sh './gradlew bootJar'

        // Java Spring Boot Gradle War
        // sh './gradlew bootWar'

        // Java Maven
        // sh 'mvn package'
        // sh 'ls target/'

        // PHP
        // sh 'composer install --optimize-autoloader --no-dev'
        // sh 'tar -zcf /tmp/tmp.tar.gz .'

        // 静态资源
        sh 'tar -zcf /tmp/tmp.tar.gz apache2/ site/'
        echo '构建完成.'
      }
    }
    stage('部署') {
      steps {
        echo '部署中...'
        script {
          // 声明服务器信息
          def remote = [:]
          remote.name = 'web-server'
          remote.allowAnyHosts = true
          remote.host = '0.0.0.0'
          remote.port = 22
          remote.user = 'ubuntu'

          // 把「CODING 凭据管理」中的「凭据 ID」填入 credentialsId，而 id_rsa 无需修改
          withCredentials([sshUserPrivateKey(credentialsId: "c4af855d-xxxx-xxxx-xxxx-f6226ae3441c", keyFileVariable: 'id_rsa')]) {
            remote.identityFile = id_rsa

            // SSH 上传文件到远端服务器
            sshPut remote: remote, from: '/tmp/tmp.tar.gz', into: '/tmp/'
            // 解压缩
            sshCommand remote: remote, command: "tar -zxf /tmp/tmp.tar.gz -C /tmp/"
            sshCommand remote: remote, sudo: true, command: "mkdir -p /var/www/example-site"
            sshCommand remote: remote, sudo: true, command: "cp -R /tmp/site/* /var/www/example-site/"
            sshCommand remote: remote, sudo: true, command: "cp -R /tmp/apache2/ /etc/"
            // 重启 apache2
            sshCommand remote: remote, sudo: true, command: "a2ensite example.com"
            sshCommand remote: remote, sudo: true, command: "a2enmod headers rewrite ssl"
            sshCommand remote: remote, sudo: true, command: "systemctl reload apache2"
          }
        }

        echo '部署完成'
      }
    }
  }
}
```

建议服务器关闭密码登录，因为可能会被暴力破解，而只允许 SSH 私钥方式登录，更安全。
如果一定要使用密码登录服务器，则使用下述 Jenkinsfile 代码：
```groovy
// 把「CODING 凭据管理」中的「凭据 ID」填入 credentialsId，而 username 和 password 无需修改
withCredentials([usernamePassword(credentialsId: "xxx", usernameVariable: 'username', passwordVariable: 'password')]) {
  remote.user = username
  remote.password = password

  sshPut remote: remote, from: '/tmp/tmp.tar.gz', into: '/tmp/'
}
```
